plugins {
    id 'com.gladed.androidgitversion' version '0.4.5'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion toolVersions.compileSdkVersion
    defaultConfig {
        applicationId "pl.jitsolutions.agile"
        minSdkVersion toolVersions.minSdkVersion
        targetSdkVersion toolVersions.targetSdkVersion
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // TODO change to final key when uploaded Google Console will be ready
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions 'version'
    productFlavors {
        // Optimized for fast development cycle
        dev {
            dimension 'version'
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
            resConfigs "en", "xxhdpi"
            versionName '1.0'
            versionCode 1
        }
        prod {
            dimension 'version'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$toolVersions.kotlinVersion"

    implementation "androidx.appcompat:appcompat:$libVersions.appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$libVersions.constraintLayoutVersion"
    implementation "com.google.android.material:material:$libVersions.materialVersion"

    testImplementation "junit:junit:$libVersions.junitVersion"

    androidTestImplementation "androidx.test:runner:$libVersions.testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$libVersions.espressoVersion"
}
