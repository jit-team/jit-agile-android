plugins {
    id 'com.gladed.androidgitversion' version '0.4.5'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion toolVersions.compileSdkVersion
    defaultConfig {
        applicationId "pl.jitsolutions.agile"
        minSdkVersion toolVersions.minSdkVersion
        targetSdkVersion toolVersions.targetSdkVersion
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'version'
    productFlavors {
        // Optimized for fast development cycle
        dev {
            dimension 'version'
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
            resConfigs "en", "xxhdpi"
            versionName '1.0'
            versionCode 1
        }
        prod {
            dimension 'version'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    def lifecycle_version = "2.0.0"
    def room_version = "2.0.0"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$toolVersions.kotlinVersion"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.appcompat:appcompat:$libVersions.appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$libVersions.constraintLayoutVersion"
    implementation "com.google.android.material:material:$libVersions.materialVersion"
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.3.0'
    implementation 'org.kodein.di:kodein-di-framework-android-x:5.3.0'

    testImplementation "junit:junit:$libVersions.junitVersion"
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

    androidTestImplementation "androidx.test:runner:$libVersions.testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$libVersions.espressoVersion"

}
